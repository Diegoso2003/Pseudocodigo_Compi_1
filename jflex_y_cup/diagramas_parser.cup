package com.example.diagramasdeflujo.analizadores;

import com.example.diagramasdeflujo.enums.TipoEnum;
import com.example.diagramasdeflujo.enums.Inst;
import com.example.diagramasdeflujo.enums.Letra;
import com.example.diagramasdeflujo.enums.Figura;
import com.example.diagramasdeflujo.backend.MensajeError;
import com.example.diagramasdeflujo.backend.Pseudocodigo;
import com.example.diagramasdeflujo.backend.OperadorMat;
import com.example.diagramasdeflujo.backend.estructuras.Bloque;
import com.example.diagramasdeflujo.backend.estructuras.Mientras;
import com.example.diagramasdeflujo.backend.estructuras.Si;
import com.example.diagramasdeflujo.backend.ReporteEstructura;
import java_cup.runtime.*;
import java.util.List;
import java.util.ArrayList;

parser code {:

    private DiagramaLexer lex;
    private List<MensajeError> syntaxErrors;
    private Pseudocodigo pseudo;

    public DiagramaParser(DiagramaLexer lex, Pseudocodigo pseudo){
        super(lex);
        this.syntaxErrors = pseudo.getErrores();
        this.pseudo = pseudo;
    }

    public void agregarTexto(StringBuilder texto, List<String> cadenas){
    	for(String cadena: cadenas){
    		texto.append(cadena);
    	}
    }
    
    public String pasarATexto(List<String> lista){
    	StringBuilder texto = new StringBuilder();
    	agregarTexto(texto, lista);
    	return texto.toString();
    }

    public DiagramaLexer getLexer(){
        return this.lex;
    }

    public List<MensajeError> getSyntaxErrors(){
        return this.syntaxErrors;
    }

    public void syntax_error(Symbol cur_token){
        MensajeError error = new MensajeError(TipoEnum.SINTACTICO);
        
        if(cur_token.value != null) {
        	error.setLexema(cur_token.value.toString());
        } else {
        	error.setLexema(symbl_name_from_id(cur_token.sym));
        }
	error.setLinea(cur_token.left);
	error.setColumna(cur_token.right);
        
        if( expected_token_ids().isEmpty()  ){
        	error.setDescripcion("ya no se esperaba ningún símbolo");
        } else {
        	StringBuilder mssBuilder = new StringBuilder();
            	mssBuilder.append("Se esperaba: ");
            	for(Integer expected_token_id : expected_token_ids() ){ 
                	if(!symbl_name_from_id(expected_token_id).equals("error")){
                    	mssBuilder.append(symbl_name_from_id(expected_token_id));
                    	mssBuilder.append(", ");
                	}
            	}
            	error.setDescripcion(mssBuilder.toString());
        }
        syntaxErrors.add(error);
    }

    public void report_error(String message, Object info){
        try{
            	Symbol cur_token = (Symbol) info;
            	MensajeError error = new MensajeError(TipoEnum.SINTACTICO);
            	if(cur_token != null){
            		error.setLinea(cur_token.left);
            		error.setColumna(cur_token.right);
        		if(cur_token.value != null){
        			error.setLexema(cur_token.value.toString());
        		} else {
        			error.setLexema(symbl_name_from_id(cur_token.sym));
        		}
            	}

            	if(message != null){
                	error.setDescripcion(message);
            	}
            syntaxErrors.add(error);
        } catch (Exception e){
        	MensajeError error = new MensajeError(TipoEnum.SINTACTICO);
        	if(message != null){
                	error.setDescripcion(message);
                	syntaxErrors.add(error);
            	}
        }
    }

    public void unrecovered_syntax_error(Symbol cur_token){
        MensajeError error = new MensajeError(TipoEnum.SINTACTICO);
        String tokenInfo = "";
    if (cur_token != null) {
        tokenInfo += cur_token.left + ", " + cur_token.right;
        // Si tiene valor asociado
        if (cur_token.value != null) {
            tokenInfo += " ('" + cur_token.value + "')";
        }
    }
        error.setDescripcion("Error que no se puede recuperar" + tokenInfo);
        syntaxErrors.add(error);
    }

:}

terminal			MAS, MENOS, POR, SIGNODIVI, PARENAPER, PARENCERRA, SEPARADOR, DEFAULT, ASIGNACION, DELIMIT, ASIGINDI, VAR, MOSTRAR, LEER,
				MIENTRAS, SI, HACER, ENTONCES, INICIO, FIN, FINMIENTRAS, FINSI, OPERALOGNEG, OPERALOG, SUMA, RESTA, MULTI, DIVI, COMA
				;
terminal DatosConfig		COLORTEXTO, COLOR, FIGURA, LETRA, LETRASIZE
				;
terminal Figura			TFIGURA
				;
terminal Integer		NUMERO
				;
terminal Double			DECIMAL
				;
terminal Letra			TLETRA
				;
terminal String			HEXADECIMAL, IDENT, CADENA, OPERARELA
				;
non terminal OperadorMat	expr, term, factor
				;
non terminal			codigo, configuracion, mientras, condicional, pseudocodigo, instrucciones, ciclo, finalmientras,
				fincondicional, instruccion_config, delimitador
				;

non terminal Bloque		bloque
				;
non terminal String		instruccion
				;
non terminal Object		color
				;
non terminal StringBuilder	condiciones, condicion
				;
non terminal List<String> 	expr2, term2, factor2
				;
			
start with codigo;

codigo ::= pseudocodigo SEPARADOR DELIMIT configuracion
	| error SEPARADOR DELIMIT configuracion							
	;
	
pseudocodigo ::= INICIO instrucciones FIN
	| INICIO error FIN									
	;
	
instrucciones ::= instrucciones instruccion:n1							{: pseudo.agregarInstruccion(n1); :}
	| instrucciones ciclo
	| ciclo
	| instruccion:n1									{: pseudo.agregarInstruccion(n1); :}
	;
	
instruccion ::= VAR IDENT:n1 DELIMIT								{: RESULT = "VAR " + n1; :}
	| VAR IDENT:n1 ASIGNACION expr2:n2 DELIMIT						{: RESULT = "VAR " + n1 + " = " + pasarATexto(n2); :}
	| MOSTRAR IDENT:n1 DELIMIT								{: RESULT = "MOSTRAR " + n1; :}
	| MOSTRAR CADENA:n1 DELIMIT								{: RESULT = "MOSTRAR " + n1; :}
	| LEER IDENT:n1 DELIMIT									{: RESULT = "LEER " + n1; :}
	| IDENT:n1 ASIGNACION expr2:n2 DELIMIT							{: RESULT = n1 + " = " + pasarATexto(n2); :}
	| error DELIMIT										{: RESULT = "Error"; :}
	;
	
ciclo ::= mientras
	| condicional
	;
	
mientras ::= MIENTRAS:n0 PARENAPER condiciones:n1 PARENCERRA HACER bloque:n2 finalmientras	{: pseudo.agregarAccion(new Mientras(n1.toString(), n2)); 
										pseudo.agregarReporteDeEstructura(new ReporteEstructura(Inst.MIENTRAS, n0left, n1.toString()));
												:}
	| MIENTRAS error finalmientras							
	;
	
condicional ::= SI:n0 PARENAPER condiciones:n1 PARENCERRA ENTONCES bloque:n2 fincondicional	{: pseudo.agregarAccion(new Si(n1.toString(), n2));
										pseudo.agregarReporteDeEstructura(new ReporteEstructura(Inst.SI, n0left, n1.toString()));
												:}
	| SI error fincondicional								
	;
	
fincondicional ::= FINSI
	| FIN SI
	;
	
bloque ::= bloque:n1 instruccion:n2								{: n1.agregarInstruccion(n2); RESULT = n1; :}
	| instruccion:n1									{: RESULT = new Bloque(n1); :}
	;
	
finalmientras ::= FINMIENTRAS
	| FIN MIENTRAS
	;
	
condiciones ::= condicion:n1 									{: RESULT = n1; :}
	| condiciones:n1 OPERALOG:n2 condicion:n3						{: RESULT = n1.append(" ").append(n2).append(" ").append(n3); :}
	;
	
condicion ::= expr2:n1 OPERARELA:n2 expr2:n3							{: 
											RESULT = new StringBuilder(pasarATexto(n1)).append(" ").append(n2).append(pasarATexto(n3)); 
												:}
	| OPERALOGNEG PARENAPER condicion:n1 PARENCERRA						{: RESULT = new StringBuilder("!(").append(n1).append(")"); :}
	;

expr2 ::= expr2:n1 SUMA:n0 term2:n2								{:  
										pseudo.agregarReporteOperador(n0left, n0right, new String[]{n1.get(n1.size()-1), "+", n2.get(0)});
										n1.add(" + "); n1.addAll(n2); RESULT = n1;:}
        | expr2:n1 RESTA:n0 term2:n2								{:  
        									pseudo.agregarReporteOperador(n0left, n0right, new String[]{n1.get(n1.size()-1), "-", n2.get(0)});
        									n1.add(" - "); n1.addAll(n2); RESULT = n1;:}
        | term2:n1										{: RESULT = n1; :}
        ;

term2 ::= term2:n1 MULTI:n0 factor2:n2								{:  
										pseudo.agregarReporteOperador(n0left, n0right, new String[]{n1.get(n1.size()-1), "*", n2.get(0)});
										n1.add(" * "); n1.addAll(n2); RESULT = n1;:}
        | term2:n1 DIVI:n0 factor2:n2								{: 
        									pseudo.agregarReporteOperador(n0left, n0right, new String[]{n1.get(n1.size()-1), "/", n2.get(0)});
        									n1.add(" / "); n1.addAll(n2); RESULT = n1;:}
        | factor2:n1										{: RESULT = n1; :}
        ;

factor2 ::= NUMERO:n1										{: RESULT = new ArrayList<>(); RESULT.add(n1.toString()); :}
	| DECIMAL:n1										{: RESULT = new ArrayList<>(); RESULT.add(n1.toString()); :}
	| IDENT:n1										{: RESULT = new ArrayList<>(); RESULT.add(n1); :}
        | PARENAPER expr2:n1 PARENCERRA								{: RESULT = new ArrayList<>(); RESULT.add("("); RESULT.addAll(n1); RESULT.add(")"); :}
        ;

configuracion ::= configuracion instruccion_config
	| instruccion_config
	;
	
instruccion_config ::= DEFAULT ASIGNACION NUMERO:n1 delimitador					{: pseudo.porDefecto(n1); :}
	| COLORTEXTO:n0 ASIGNACION color:n1 ASIGINDI NUMERO:n2 delimitador			{: pseudo.cambiarColorTexto(n1, n2, n0.getTipo()); :}
	| COLOR:n0 ASIGNACION color:n1 ASIGINDI NUMERO:n2 delimitador				{: pseudo.cambiarColor(n1, n2, n0.getTipo()); :}
	| FIGURA:n0 ASIGNACION TFIGURA:n1 ASIGINDI NUMERO:n2 delimitador			{: pseudo.cambiarFigura(n1, n2, n0.getTipo()); :}
	| LETRA:n0 ASIGNACION TLETRA:n1 ASIGINDI NUMERO:n2 delimitador				{: pseudo.cambiarLetra(n1, n2, n0.getTipo()); :}
	| LETRASIZE:n0 ASIGNACION expr:n1 ASIGINDI NUMERO:n2 delimitador			{: pseudo.cambiarTamaño(n1.getNumero(), n2, n0.getTipo()); :}
	| error DELIMIT									
	;
	
delimitador ::= DELIMIT
	| delimitador DELIMIT
	;
	
color ::= HEXADECIMAL:n1									{: RESULT = pseudo.hexaAColor("#"+n1.substring(1)); :}
	| expr:n1 COMA expr:n2 COMA expr:n3							{: RESULT = pseudo.rgbAColor(n1.getNumero(), n2.getNumero(), n3.getNumero()); :}
	;
			
expr ::= expr:n1 MAS:n0 term:n2									{: RESULT = new OperadorMat(n1.getNumero() + n2.getNumero()); 
												RESULT.setUltimo(n2.getUltimo()); RESULT.setPrimero(n1.getPrimero()); 
										pseudo.agregarReporteOperador(n0left, n0right, new String[]{n1.getUltimo(), "+", n2.getPrimero()});:}
        | expr:n1 MENOS:n0 term:n2									{: RESULT = new OperadorMat(n1.getNumero() - n2.getNumero()); 
												RESULT.setUltimo(n2.getUltimo()); RESULT.setPrimero(n1.getPrimero()); 
										pseudo.agregarReporteOperador(n0left, n0right, new String[]{n1.getUltimo(), "-", n2.getPrimero()});:}
        | term:n1										{: RESULT = n1; :}
        ;

term ::= term:n1 POR:n0 factor:n2								{: RESULT = new OperadorMat(n1.getNumero() * n2.getNumero());
												RESULT.setUltimo(n2.getUltimo()); RESULT.setPrimero(n1.getPrimero());
										pseudo.agregarReporteOperador(n0left, n0right, new String[]{n1.getUltimo(), "*", n2.getPrimero()});:}
        | term:n1 SIGNODIVI:n0 factor:n2							{: RESULT = new OperadorMat(n1.getNumero() / n2.getNumero()); 
												RESULT.setUltimo(n2.getUltimo()); RESULT.setPrimero(n1.getPrimero()); 
										pseudo.agregarReporteOperador(n0left, n0right, new String[]{n1.getUltimo(), "/", n2.getPrimero()});:}
        | factor:n1										{: RESULT = n1; :}
        ;

factor ::= NUMERO:n1										{: RESULT = new OperadorMat(n1); :}
	| DECIMAL:n2										{: RESULT = new OperadorMat(n2); :}
        | PARENAPER expr:n1 PARENCERRA								{: RESULT = n1; n1.setUltimo(")"); n1.setPrimero("("); :}
        ;
