package com.example.diagramasdeflujo.analizadores;

import com.example.diagramasdeflujo.enums.TipoEnum;
import com.example.diagramasdeflujo.enums.Inst;
import com.example.diagramasdeflujo.backend.MensajeError;
import com.example.diagramasdeflujo.backend.Pseudocodigo;
import com.example.diagramasdeflujo.backend.estructuras.Bloque;
import com.example.diagramasdeflujo.backend.estructuras.Mientras;
import com.example.diagramasdeflujo.backend.estructuras.Si;
import com.example.diagramasdeflujo.backend.ReporteEstructura;
import java_cup.runtime.*;
import java.util.List;
import java.util.ArrayList;

parser code {:

    private DiagramaLexer lex;
    private List<MensajeError> syntaxErrors;
    private Pseudocodigo pseudo;

    public DiagramaParser(DiagramaLexer lex, List<MensajeError> syntaxErrors, Pseudocodigo pseudo){
        super(lex);
        this.syntaxErrors = syntaxErrors;
        this.pseudo = pseudo;
    }

    public void agregarTexto(StringBuilder texto, List<String> cadenas){
    	for(String cadena: cadenas){
    		texto.append(cadena);
    	}
    }
    
    public String pasarATexto(List<String> lista){
    	StringBuilder texto = new StringBuilder();
    	agregarTexto(texto, lista);
    	return texto.toString();
    }

    public DiagramaLexer getLexer(){
        return this.lex;
    }

    public List<MensajeError> getSyntaxErrors(){
        return this.syntaxErrors;
    }

    public void syntax_error(Symbol cur_token){
        MensajeError error = new MensajeError(TipoEnum.SINTACTICO);
        
        if(cur_token.value != null) {
        	error.setLexema(cur_token.value.toString());
        } else {
        	error.setLexema(symbl_name_from_id(cur_token.sym));
        }
	error.setLinea(cur_token.left);
	error.setColumna(cur_token.right);
        
        if( expected_token_ids().isEmpty()  ){
        	error.setDescripcion("ya no se esperaba ningún símbolo");
        } else {
        	StringBuilder mssBuilder = new StringBuilder();
            	mssBuilder.append("Se esperaba: ");
            	for(Integer expected_token_id : expected_token_ids() ){ 
                	if(!symbl_name_from_id(expected_token_id).equals("error")){
                    	mssBuilder.append(symbl_name_from_id(expected_token_id));
                    	mssBuilder.append(", ");
                	}
            	}
            	error.setDescripcion(mssBuilder.toString());
        }
    }

    public void report_error(String message, Object info){
        try{
            	Symbol cur_token = (Symbol) info;
            	MensajeError error = new MensajeError(TipoEnum.SINTACTICO);
            	if(cur_token != null){
            		error.setLinea(cur_token.left);
            		error.setColumna(cur_token.right);
        		if(cur_token.value != null){
        			error.setLexema(cur_token.value.toString());
        		} else {
        			error.setLexema(symbl_name_from_id(cur_token.sym));
        		}
            	}

            	if(message != null){
                	error.setDescripcion(message);
            	}
            syntaxErrors.add(error);
        } catch (Exception e){
        	MensajeError error = new MensajeError(TipoEnum.SINTACTICO);
        	if(message != null){
                	error.setDescripcion(message);
                	syntaxErrors.add(error);
            	}
        }
    }

    public void unrecovered_syntax_error(Symbol cur_token){
        MensajeError error = new MensajeError(TipoEnum.SINTACTICO);
        error.setDescripcion("Error que no se puede recuperar");
        syntaxErrors.add(error);
    }

:}

terminal			MAS, MENOS, POR, SIGNODIVI, PARENAPER, PARENCERRA, SEPARADOR, DEFAULT, ASIGNACION, DELIMIT, ASIGINDI, VAR, MOSTRAR, LEER,
				MIENTRAS, SI, HACER, ENTONCES, INICIO, FIN, FINMIENTRAS, FINSI, OPERALOGNEG, OPERALOG, SUMA, RESTA, MULTI, DIVI, COMA
				;
terminal Integer		NUMERO, COLORTEXTO, COLOR, FIGURA, LETRA, TFIGURA, TLETRA, LETRASIZE
				;
terminal Double			DECIMAL
				;
terminal String			HEXADECIMAL, IDENT, CADENA, OPERARELA
				;
non terminal Double		expr, term, factor
				;
non terminal			codigo, configuracion, mientras, condicional, pseudocodigo, instrucciones, ciclo, finalmientras,
				fincondicional, instruccion_config
				;
non terminal Bloque		bloque
				;
non terminal String		color, instruccion
				;
non terminal StringBuilder	condiciones, condicion
				;
non terminal List<String> 	expr2, term2, factor2
				;
			
start with codigo;

codigo ::= pseudocodigo SEPARADOR configuracion
	| error SEPARADOR configuracion
	;
	
pseudocodigo ::= INICIO instrucciones FIN
	;
	
instrucciones ::= instrucciones instruccion:n1							{: pseudo.agregarInstruccion(n1); :}
	| instrucciones ciclo
	| ciclo
	| instruccion:n1									{: pseudo.agregarInstruccion(n1); :}
	;
	
instruccion ::= VAR IDENT:n1 DELIMIT								{: RESULT = "VAR " + n1; :}
	| VAR IDENT:n1 ASIGNACION expr2:n2 DELIMIT						{: RESULT = "VAR " + n1 + " = " + pasarATexto(n2); :}
	| MOSTRAR IDENT:n1 DELIMIT								{: RESULT = "MOSTRAR " + n1; :}
	| MOSTRAR CADENA:n1 DELIMIT								{: RESULT = "MOSTRAR " + n1; :}
	| LEER IDENT:n1 DELIMIT									{: RESULT = "LEER " + n1; :}
	| error DELIMIT
	;
	
ciclo ::= mientras
	| condicional
	;
	
mientras ::= MIENTRAS:n0 PARENAPER condiciones:n1 PARENCERRA HACER bloque:n2 finalmientras	{: pseudo.agregarAccion(new Mientras(n1.toString(), n2)); 
										pseudo.agregarReporteDeEstructura(new ReporteEstructura(Inst.MIENTRAS, n0left, n1.toString()));
												:}
	;
	
condicional ::= SI:n0 PARENAPER condiciones:n1 PARENCERRA ENTONCES bloque:n2 fincondicional	{: pseudo.agregarAccion(new Si(n1.toString(), n2));
										pseudo.agregarReporteDeEstructura(new ReporteEstructura(Inst.MIENTRAS, n0left, n1.toString()));
												:}
	;
	
fincondicional ::= FINSI
	| FIN SI
	;
	
bloque ::= bloque:n1 instruccion:n2								{: n1.agregarInstruccion(n2); RESULT = n1; :}
	| instruccion:n1									{: RESULT = new Bloque(n1); :}
	;
	
finalmientras ::= FINMIENTRAS
	| FIN MIENTRAS
	;
	
condiciones ::= condicion:n1 OPERALOG:n2 condicion:n3						{: RESULT = n1.append(" ").append(n2).append(" ").append(n3); :}
	| condiciones:n1 OPERALOG:n2 condicion:n3						{: RESULT = n1.append(" ").append(n2).append(" ").append(n3); :}
	;
	
condicion ::= expr2:n1 OPERARELA:n2 expr2:n3							{: 
											RESULT = new StringBuilder(pasarATexto(n1)).append(" ").append(n2).append(pasarATexto(n3)); 
												:}
	| OPERALOGNEG PARENAPER condicion:n1 PARENCERRA						{: RESULT = new StringBuilder("!(").append(n1).append(")"); :}
	;

expr2 ::= expr2:n1 SUMA term2:n2								{: n1.add(" + "); n1.addAll(n2); RESULT = n1; :}
        | expr2:n1 RESTA term2:n2								{: n1.add(" - "); n1.addAll(n2); RESULT = n1; :}
        | term2:n1										{: RESULT = n1; :}
        ;

term2 ::= term2:n1 MULTI factor2:n2								{: n1.add(" * "); n1.addAll(n2); RESULT = n1; :}
        | term2:n1 DIVI factor2:n2								{: n1.add(" / "); n1.addAll(n2); RESULT = n1; :}
        | factor2:n1										{: RESULT = n1; :}
        ;

factor2 ::= NUMERO:n1										{: RESULT = new ArrayList<>(); RESULT.add(n1.toString()); :}
	| DECIMAL:n1										{: RESULT = new ArrayList<>(); RESULT.add(n1.toString()); :}
	| IDENT:n1										{: RESULT = new ArrayList<>(); RESULT.add(n1); :}
        | PARENAPER expr2:n1 PARENCERRA								{: RESULT = new ArrayList<>(); RESULT.add("("); RESULT.addAll(n1); RESULT.add(")"); :}
        ;

configuracion ::= configuracion instruccion_config
	| instruccion_config
	;
	
instruccion_config ::= DEFAULT ASIGNACION NUMERO DELIMIT
	| COLORTEXTO ASIGNACION color ASIGINDI NUMERO DELIMIT
	| COLOR ASIGNACION color ASIGINDI NUMERO DELIMIT
	| FIGURA ASIGNACION TFIGURA ASIGINDI NUMERO DELIMIT
	| LETRA ASIGNACION TLETRA ASIGINDI NUMERO DELIMIT
	| LETRASIZE ASIGNACION expr ASIGINDI NUMERO DELIMIT
	| error DELIMIT
	;
	
color ::= HEXADECIMAL
	| expr COMA expr COMA expr
	;
			
expr ::= expr MAS term
        | expr MENOS term
        | term
        ;

term ::= term POR factor
        | term SIGNODIVI factor
        | factor
        ;

factor ::= NUMERO
	| DECIMAL
        | PARENAPER expr PARENCERRA
        ;
